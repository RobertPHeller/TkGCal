#*****************************************************************************
#
#  System        : 
#  Module        : 
#  Object Name   : $RCSfile$
#  Revision      : $Revision$
#  Date          : $Date$
#  Author        : $Author$
#  Created By    : Robert Heller
#  Created       : Sat Apr 13 13:35:58 2024
#  Last Modified : <240415.1053>
#
#  Description	
#
#  Notes
#
#  History
#	
#*****************************************************************************
## @copyright
#    Copyright (C) 2024  Robert Heller D/B/A Deepwoods Software
#			51 Locke Hill Road
#			Wendell, MA 01379-9728
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
# @file TkGCal.tcl
# @author Robert Heller
# @date Sat Apr 13 13:35:58 2024
# 
#
#*****************************************************************************


package require snit
package require Tk
package require tile
package require MainWindow
package require ScrollWindow
package require ROText
package require IconImage
package require Dialog
package require LabelFrames

snit::type AddEventDialog {
    pragma -hastypeinfo    no
    pragma -hastypedestroy no
    pragma -hasinstances   no
    
    typecomponent _addEventDialog
    typecomponent _remind_time
    typecomponent _remind_time_units
    typecomponent _remind_method
    typemethod GetRemind {} {
        set time "[$_remind_time get][$_remind_time_units get]"
        set meth "[$_remind_method get]"
        if {$meth eq ""} {
            return $time
        } else {
            return "$time $meth"
        }
    }
    typevariable _remind_default 0
    typemethod GetRemindDefault {} {
        return $_remind_default
    }
    typecomponent _color
    typemethod GetColor {} {
        return [$_color get]
    }
    typecomponent _title
    typemethod GetTitle {} {
        return [$_title get]
    }
    typecomponent _who
    typemethod GetWho {} {
        return [$_who get]
    }
    typecomponent _where
    typemethod GetWhere {} {
        return [$_where get]
    }
    typecomponent _when
    typemethod GetWhen {} {
        return [$_when get]
    }
    typecomponent _duration
    typemethod GetDuration {} {
        return [$_duration get]
    }
    typecomponent _description
    typemethod GetDescription {} {
        return [string trim [$_description get 1.0 end]]
    }
    typevariable _allday 0
    typemethod GetAllDay {} {
        return $_allday
    }
    typevariable _calendar
    typemethod GetCalendar {} {
        return $_calendar
    }
    typemethod createDialog {window args} {
        set calendars [list]
        foreach l [lrange [split [exec {*}[TkGCal::ListCommand]] "\n"] 2 end] {
            if {[regexp {^[[:space:]]*([^[:space:]]+)[[:space:]]+(.*)$} \
                 $l => access title] > 0} {
                if {$access eq "reader"} {continue}
                lappend calendars $title
            }
        }
        set _addEventDialog [Dialog .addEventDialog -title "Add New Event" \
                             -modal local -bitmap questhead \
                             -parent [from args -parent .] -transient yes \
                             -default ok -cancel cancel]
        $_addEventDialog add ok -text Ok
        $_addEventDialog add cancel -text Cancel
        set dframe [$_addEventDialog getframe]
        set _title [LabelEntry $dframe.title -label "Title"]
        pack $_title -fill x
        set _who [LabelEntry $dframe.who -label "Who"]
        pack $_who -fill x
        set _where [LabelEntry $dframe.where -label "Where"]
        pack $_where -fill x
        set _when [LabelEntry $dframe.when -label "When"]
        pack $_when -fill x
        set _duration [LabelEntry $dframe.duration -label "Duration"]
        pack $_duration -fill x
        set allday [ttk::checkbutton $dframe.allday \
                    -variable [mytypevar _allday] -text "All day?"]
        pack $allday -fill x
        set lf [LabelFrame $dframe.remindFrame -text "Reminders"]
        pack $lf -fill x
        set _color [LabelComboBox $dframe.color -label "Color" \
                    -values {lavender sage grape flamingo banana tangerine \
                    peacock graphite blueberry basil tomato}]
        pack $_color -fill x
        set lff [$lf getframe]
        set _remind_time [ttk::spinbox $lff.time -from 1 -to 200 -increment 1]
        pack $_remind_time -side left -fill x
        set _remind_time_units [ttk::combobox $lff.units -values {w d h m}]
        pack $_remind_time_units -side left
        set _remind_method [ttk::combobox $lff.meth -values {popup email sms}]
        pack $_remind_method -side left
        set remind_default [ttk::checkbutton $lff.default \
                            -text "Use Defaults?" \
                            -variable [mytypevar _remind_default]]
        pack $remind_default -side left
        set descrlf [ttk::labelframe $dframe.descrlf -labelanchor nw \
                     -text "Description"]
        pack $descrlf -expand yes -fill both
        set _description [text $descrlf.description -width 40 -height 5]
        pack $_description -expand yes -fill both
        set calendarlf [ttk::labelframe $dframe.calendarlf -labelanchor nw \
                     -text "Calendar"]
        pack $calendarlf
        set i 0
        foreach c $calendars {
            incr i
            pack [ttk::radiobutton $calendarlf.cal$i -text $c -value $c \
                  -variable [mytypevar _calendar]] -fill x
        }
        set _calendar [lindex $calendars 0]
        return $_addEventDialog
    }
    
}

    

snit::widget TkGCal {
    typevariable GCALCLI {@GCALCLI@}
    proc filterVT100 {string} {
        set string [regsub -all [format {%c\(0q%c\(B} 27 27] $string {─}]
        set string [regsub -all [format {%c\(0x%c\(B} 27 27] $string {│}]
        set string [regsub -all [format {%c\(0j%c\(B} 27 27] $string {┘}]
        set string [regsub -all [format {%c\(0k%c\(B} 27 27] $string {┐}]
        set string [regsub -all [format {%c\(0l%c\(B} 27 27] $string {┌}]
        set string [regsub -all [format {%c\(0m%c\(B} 27 27] $string {└}]
        set string [regsub -all [format {%c\(0n%c\(B} 27 27] $string {┼}]
        set string [regsub -all [format {%c\(0t%c\(B} 27 27] $string {├}]
        set string [regsub -all [format {%c\(0u%c\(B} 27 27] $string {┤}]
        set string [regsub -all [format {%c\(0v%c\(B} 27 27] $string {┴}]
        set string [regsub -all [format {%c\(0w%c\(B} 27 27] $string {┬}]
        return $string
    }
    proc AgendaCommandTsv {start end} {
        return [list $GCALCLI --nocolor agenda --tsv $start $end]
    }
    proc AgendaCommand {start end args} {
        return [list $GCALCLI --nocolor agenda --width [from args -width 80] --details end $start $end]
    }
    proc AgendaAllCommand {start end} {
        return [list $GCALCLI --nocolor agenda --details all $start $end]
    }
    proc CalWeekCommand {start weeks} {
        return [list $GCALCLI --nocolor calw $start $weeks]
    }
    proc ReminderCommand {minutes} {
        return [list $GCALCLI remind --use-reminders 15]
    }
    proc AddCommand {args} {
        return [list  $GCALCLI add --noprompt {*}$args]
    }
    proc ListCommand {} {
        return [list  $GCALCLI --nocolor list]
    }
    typecomponent mainwindow
    typecomponent calendar
    typecomponent daysagenda
    typecomponent   daysagenda_date
    typecomponent   daysagenda_agenda
    typevariable ImageDir
    typeconstructor {
        set ImageDir [file join [file dirname [file dirname [info script]]] Common]
        #puts stderr "*** typeconstructor: ImageDir is $ImageDir"
        set leftArrow [image create bitmap  -file [file join $ImageDir left.xbm]]
        set rightArrow [image create bitmap  -file [file join $ImageDir right.xbm]]
        ttk::style layout flatbutton [ttk::style layout TButton]
        foreach {o v} [ttk::style configure TButton] {
            ttk::style configure flatbutton $o $v
        }
        ttk::style configure flatbutton -relief flat
        ttk::style layout headlabel [ttk::style layout TLabel]
        foreach {o v} [ttk::style configure TLabel] {
            ttk::style configure headlabel $o $v
        }
        ttk::style configure headlabel -font [font create -family Times \
                                              -size 20 -weight bold]
        ttk::style layout weekendlabel [ttk::style layout TLabel]
        foreach {o v} [ttk::style configure TLabel] {
            ttk::style configure weekendlabel $o $v
        }
        ttk::style configure weekendlabel \
              -font [font create -family Times -slant italic] \
              -foreground red
        ttk::style layout weekdaylabel [ttk::style layout TLabel]
        foreach {o v} [ttk::style configure TLabel] {
            ttk::style configure weekdaylabel $o $v
        }
        ttk::style configure weekdaylabel -font [font create -family Times \
                                            -slant roman]
        ttk::style layout weekendbutton [ttk::style layout TButton]
        ttk::style configure weekendbutton -relief flat
        ttk::style configure weekendbutton -font [font create -family Times -slant italic] \
              -foreground blue
        ttk::style layout weekdaybutton [ttk::style layout TButton]
        ttk::style configure weekdaybutton -relief flat
        ttk::style configure weekdaybutton -font [font create -family Times -slant roman] \
              -foreground blue
        
        global argv
        global argv0
        if {[set index [lsearch -exact $argv -popup]] >= 0} {
            set argv [lreplace $argv $index $index]
            $type PopupAgenda [from argv -geometry {}]
            return
        }
        set mainwindow [mainwindow .mainwindow -scrolling false]
        pack $mainwindow -expand yes -fill both
        wm title [winfo toplevel $mainwindow] "Calendar"
        wm protocol [winfo toplevel $mainwindow] WM_DELETE_WINDOW ::exit
        set daysagenda [$mainwindow slideout add DaysAgenda]
        set daysagenda_date [ttk::labelframe $daysagenda.date -labelanchor n \
                             -text "The Date"]
        pack $daysagenda_date -expand yes -fill both
        set scrollw [ScrolledWindow $daysagenda_date.scrollw \
                     -scrollbar both -auto both]
        pack $scrollw -expand yes -fill both
        set daysagenda_agenda [ROText [$scrollw getframe].agenda -width 100]
        $scrollw setwidget $daysagenda_agenda
        pack [ttk::button $daysagenda.close -text "Close" \
              -command [list $mainwindow slideout hide DaysAgenda]] \
              -expand yes -fill x
        set frame [$mainwindow scrollwindow getframe]
        set calendar [TkGCal create $frame.tkgcal \
                      -labelframe $daysagenda_date \
                      -rotext $daysagenda_agenda \
                      -showagenda [list $mainwindow slideout show DaysAgenda]]
        $mainwindow scrollwindow setwidget $calendar
        $mainwindow toolbar add Tools
        $mainwindow toolbar show Tools
        $mainwindow toolbar addbutton Tools add -text "Add Event" \
              -image [IconImage image new] -command [mytypemethod _addEvent]
        $mainwindow menu entryconfigure file "Close" \
              -command [list wm iconify [winfo toplevel $mainwindow]]
        $mainwindow menu entryconfigure file "Exit" \
              -command ::exit
        set geo [from argv -geometry {}]
        $mainwindow showit
        if {$geo ne {}} {
            puts stderr "*** geo is \{$geo\}"
            update idle
            wm geometry . $geo
        }
        if {[set index [lsearch -exact $argv -iconic]] >= 0} {
            set argv [lreplace $argv $index $index]
            wm iconify [winfo toplevel $mainwindow]
        }
        $type Notify
    }
    typemethod PopupAgenda {{geo {}}} {
        wm title . "Agenda"
        wm protocol . WM_DELETE_WINDOW ::exit
        set scrollw [ScrolledWindow .agendaSW -scrollbar both -auto both]
        pack $scrollw -expand yes -fill both
        set agenda [ROText [$scrollw getframe].agenda -width 60]
        $scrollw setwidget $agenda
        set afp [open "|[AgendaCommand now +5d -width 60]" "r"]
        fileevent $afp readable [mytypemethod fillagenda $afp $agenda]
        set dismis [ttk::button .dismis -text Dismis -command ::exit]
        pack $dismis -anchor center
        if {$geo ne {}} {
            update idle
            wm geometry . $geo
        }
    }
    typemethod fillagenda {fp text} {
        if {[gets $fp line] < 0} {
            catch {close $fp}
        } else {
            $text insert end "[filterVT100 $line]\n"
        }
    }
    typemethod Notify {} {
        catch [list exec {*}[ReminderCommand 15] &]
        set afp [open "|[AgendaCommandTsv now +15m]" r]
        fileevent $afp readable [mytypemethod Notify_popup $afp]
        after 300000 [mytypemethod Notify]
    }
    #                          startdate                 starttime             enddate                      endtime                    title   
    typevariable TSVPattern {^([^[:space:]]+)[[:space:]]([^[:space:]]+)[[:space:]]([^[:space:]]+)[[:space:]]([^[:space:]]+)[[:space:]](.*)$}
    typemethod Notify_popup {fp} {
        if {[gets $fp line] < 0} {
            catch {close $fp}
        } else {
            if {[regexp $TSVPattern $line => \
                 startdate starttime enddate endtime title] > 0} {
                set future [expr {[clock scan "$startdate $starttime" -format {%Y-%m-%d %H:%M}] - [clock seconds]}]
                if {$future > 0 && $future < [expr {15 * 60}]} {
                    $type notice_popup $startdate $starttime $title
                }
            }
        }
    }
    typevariable notice_popupIndex 0
    typemethod notice_popup {date time title} {
        incr notice_popupIndex
        set noticePopup [toplevel .noticePopup$notice_popupIndex]
        set message [message $noticePopup.message \
                     -text [format "%s\n\n%s" $title $time]]
        pack $message -expand yes -fill both
        set button [ttk::button $noticePopup.dismis \
                    -text Dismis -command \
                    -command [list destroy $noticePopup]]
        pack $button -anchor center
        wm deiconify $noticePopup
        update idle
        wm transient $noticePopup [winfo toplevel $mainwindow]
    }
                    
    typecomponent _AddEventDialog
    
    typemethod _Create_AddEventDialog {} {
        if {[info exists _AddEventDialog] &&
            [winfo exists $_AddEventDialog]} {
            return $_AddEventDialog
        }
        set _AddEventDialog [AddEventDialog createDialog \
                             $mainwindow.addEventDialog -parent $mainwindow]
        return $_AddEventDialog
    }
        
    typemethod _addEvent {} {
        set result [[$type _Create_AddEventDialog] draw]
        #puts stderr "*** $type _addEvent: result is $result"
        if {$result == 0} {
            set title [AddEventDialog GetTitle]
            if {$title eq ""} {return}
            lappend arglist --title $title
            set who  [AddEventDialog GetWho]
            if {$who ne ""} {
                lappend arglist --who $who
            }
            set where [AddEventDialog GetWhere]
            if {$where ne ""} {
                lappend arglist --where $where
            }
            set when  [AddEventDialog GetWhen]
            if {$when eq ""} {return}
            lappend arglist --when $when
            set duration [AddEventDialog GetDuration]
            if {$duration ne ""} {
                lappend arglist --duration $duration
            }
            if {[AddEventDialog GetAllDay]} {
                lappend arglist --allday
            }
            set description [AddEventDialog GetDescription]
            if {$description ne {}} {
                lappend arglist --description $description
            }
            set color [AddEventDialog GetColor]
            if {$color ne {}} {
                lappend arglist --color $color
            }
            set remind [AddEventDialog GetRemind]
            if {$remind ne {}} {
                lappend arglist --reminder $remind
            } elseif {[AddEventDialog GetRemindDefault]} {
                lappend arglist --default-reminders
            }
            set calendar [AddEventDialog GetCalendar]
            lappend arglist --calendar $calendar
            set cmd [AddCommand {*}$arglist]
            #puts stderr "*** $type _addEvent: cmd is $cmd"
            if {[catch {exec {*}$cmd} result]} {
                tk_messageBox -type ok -icon error -message $result
            }
        }
    }
    component month
    component year
    component calframe
    typevariable leftArrow
    typevariable rightArrow
    variable currentMonth
    variable currentYear
    option -xscrollcommand
    option -yscrollcommand
    option -labelframe -default {}
    option -rotext -default {}
    option -showagenda -default {}
    constructor {args} {
        set currentMonth [clock format [clock scan now] -format %B]
        set currentYear [clock format [clock scan now] -format %Y]
        set headframe [ttk::frame $win.headframe -relief groove -borderwidth 3]
        pack $headframe -fill x
        set prevmonth [ttk::button $headframe.prevmonth \
                       -style flatbutton -command [mymethod _prevMonth] \
                       -image $leftArrow]
        pack $prevmonth -side left
        install month using ttk::label $headframe.month \
              -textvariable [myvar currentMonth] -style headlabel \
              -justify center -anchor center
        pack $month -side left -fill x -expand yes
        set nextmonth [ttk::button $headframe.nextmonth \
                       -style flatbutton -command [mymethod _nextMonth] \
                       -image $rightArrow]
        pack $nextmonth -side left
        set prevyear [ttk::button $headframe.prevyear \
                      -style flatbutton -command [mymethod _prevYear] \
                      -image $leftArrow]
        pack $prevyear -side left
        install year using ttk::label $headframe.year \
              -textvariable [myvar currentYear]  -style headlabel \
              -justify center -anchor center
        pack $year -side left -fill x -expand yes
        set nextyear [ttk::button $headframe.nextyear \
                      -style flatbutton -command [mymethod _nextYear] \
                      -image $rightArrow]
        pack $nextyear -side left
        install calframe using ttk::frame $win.calframe \
              -relief groove -borderwidth 3 -padding {50 0 50 50}
        pack $calframe -fill both 
        foreach d {Sun Mon Tue Wed Thu Fri Sat} i {0 1 2 3 4 5 6} \
              s {weekendlabel weekdaylabel weekdaylabel weekdaylabel weekdaylabel weekdaylabel weekendlabel} {\
              grid configure [ttk::label $calframe.[string tolower $d] \
                              -text $d -style $s] -column $i -row 0 -sticky news
        }
        $self configurelist $args
        $self _populateCurrentMonth
    }
    method _populateCurrentMonth {} {
        set rows [lindex [grid size $calframe] 1]
        if {$rows > 1} {
            for {set irow [expr {$rows - 1}]} {$irow > 0} {incr irow -1} {
                foreach s [grid slaves $calframe -row $irow] {
                    destroy $s
                }
            }
        }
        set day [clock scan [format "%s/1/%d" $currentMonth $currentYear] -format {%B/%d/%Y}]
        set start [clock format $day -format {%m/%d/%Y}]
        set lastday [clock add $day 1 month]
        set end [clock format $lastday -format {%m/%d/%Y}]
        set cmd [AgendaCommand $start $end]
        #puts stderr "*** $self _populateCurrentMonth: cmd is \{$cmd\}"
        set dates [eval [concat exec $cmd]]
        #puts stderr "*** $self _populateCurrentMonth: dates is \{$dates\}"
        set gridrow 1
        while {[clock format $day -format %B] eq $currentMonth} {
            set dayofweek [clock format $day -format %w]
            set date      [clock format $day -format %e]
            set day1 [clock add $day 1 day]
            set pattern [clock format $day -format {^%a %b %d}]
            #puts stderr "*** $self _populateCurrentMonth: pattern is \{$pattern\}"
            if {[regexp -line $pattern $dates] < 1} {
                # no events
                if {$dayofweek == 0 || $dayofweek == 6} {
                    set style weekendlabel
                } else {
                    set style weekdaylabel
                }
                grid configure [ttk::label $calframe.d_$date \
                                -text $date -anchor e -style $style] \
                      -row $gridrow -column $dayofweek
            } else {
                # has events
                if {$dayofweek == 0 || $dayofweek == 6} {
                    set style weekendbutton
                } else {
                    set style weekdaybutton
                }
                grid configure [ttk::button $calframe.d_$date \
                                -text $date -style $style \
                                -command [mymethod _showdate $day]] \
                       -row $gridrow -column $dayofweek
            }
            if {$dayofweek == 6} {incr gridrow}
            set day $day1
        }
    }
    method _prevMonth {} {
        set day [clock scan [format "%s/1/%d" $currentMonth $currentYear] -format {%B/%d/%Y}]
        set day [clock add $day -1 month]
        set currentMonth [clock format $day -format %B]
        set currentYear  [clock format $day -format %Y]
        $self _populateCurrentMonth
    }
    method _nextMonth {} {
        set day [clock scan [format "%s/1/%d" $currentMonth $currentYear] -format {%B/%d/%Y}]
        set day [clock add $day 1 month]
        set currentMonth [clock format $day -format %B]
        set currentYear  [clock format $day -format %Y]
        $self _populateCurrentMonth
    }
    method _prevYear {} {
        incr currentYear -1
        $self _populateCurrentMonth
    }
    method _nextYear {} {
        incr currentYear
        $self _populateCurrentMonth
    }
    variable agendaFP
    variable agendaDone
    method _showdate {day} {
        if {$options(-labelframe) ne {}} {
            $options(-labelframe) configure \
                  -text [clock format $day -format {%A %B %e, %Y}]
        }
        set tomorrow [clock add $day 1 day]
        set start [clock format $day -format {%m/%d/%Y}]
        set end   [clock format $tomorrow -format {%m/%d/%Y}]
        if {$options(-rotext) ne {}} {
            $options(-rotext) delete 1.0 end
        }
        set agendaDone 0
        set agendaFP [open "|[AgendaAllCommand $start $end]" r]
        fileevent $agendaFP readable [mymethod _processAgendaPipe]
        tkwait variable agendaDone
    }
    method _processAgendaPipe {} {
        if {[gets $agendaFP line] < 0} {
            catch {close $agendaFP}
            if {$options(-showagenda) ne {}} {
                uplevel #0 $options(-showagenda)
            }
            incr agendaDone
        }
        if {$options(-rotext) ne {}} {
            $options(-rotext) insert end "[filterVT100 $line]\n"
        }
    }
}
